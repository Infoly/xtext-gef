/*******************************************************************************
 * Copyright (c) 2015 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.xtext.example.statemachine.Statemachine with org.eclipse.xtext.common.Terminals

generate statemachine "http://xtext.org/example/Statemachine"

Statemachine :
    {Statemachine}
	(states+=State | transitions+=Transition)*;

State:
	initial?='initial'? final?='final'? 'state' id=ID name=STRING?
		('actions' '{' actions+=Command* '}')?
	'end';

Transition:
	sourceState=[State|ID] '=>' targetState=[State|ID] ('(' event=Expression? ')')?;

Command:
	SetCommand | ExecuteCommand | PrintCommand;

SetCommand:
	'set' signal=ID '=' value=Expression;

ExecuteCommand:
	'execute' operation=ID '(' (arguments+=Expression (',' arguments+=Expression)*)? ')';

PrintCommand:
	'print' value=Expression;

Expression:
	VerbatimExpression | StatePropertyExpression;

VerbatimExpression:
	code=STRING;

StatePropertyExpression:
	state=[State|ID] '.' property=ID;
